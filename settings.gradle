rootProject.name = 'sprout'

[
        'module-common',
        'module-member',
        'module-secure',
        'module-web',
        'module-community-history',
].each { module ->
    def moduleDir = new File(rootDir, module)
    if (!moduleDir.exists()) {
        moduleDir.mkdirs()
    }

    def buildScript = new File(moduleDir.absolutePath, 'build.gradle')
    if (!buildScript.exists()) {
        buildScript.text =
                """
                    dependencies {
                    
                    }
                    
                    bootJar {
                        enabled = false;
                    }
                    
                    jar {
                        enabled = true;
                    }
                """.stripIndent(20).trim()
    }

    [
            'src/main/java/com/ark/sprout',
            'src/main/resources',
            'src/test/java/com/ark/sprout',
            'src/test/resources',
    ].each { source ->
        def srcDir = new File(moduleDir.absolutePath, source)
        if (!srcDir.exists()) {
            srcDir.mkdirs()
        }
    }

    def projectName = ":$moduleDir.name"
    include projectName
    project(projectName).projectDir = moduleDir
}
