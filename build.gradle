plugins {
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'java'
}

group = 'team.ark'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
    }
}

tasks.withType(JavaCompile) {
    options.fork = true
}

ext {
    set('snippetsDir', file('build/generated-snippets'))
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// Not create sprout-plain.jar
jar {
    enabled = false
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor

    from file('build/asciidoc/html5/index.html')
    into file('src/main/resources/static/docs')
}

bootRun {
    systemProperty 'jasypt.encryptor.password', System.getenv('JASYPT_PASSWORD')
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    systemProperty 'jasypt.encryptor.password', System.getenv('JASYPT_PASSWORD')
}

sonarqube {
    properties {
        property "sonar.projectKey", "TEAM-ARK_sprout-backend"
        property "sonar.organization", "team-ark"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

build {
    dependsOn copyDocument
}
